import com.google.devtools.ksp.gradle.KspAATask
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
	alias(libs.plugins.kotlinMultiplatform)
	alias(libs.plugins.kotlinMultiplatformLibrary)
	alias(libs.plugins.kotlin.serialization)
	alias(libs.plugins.composeMultiplatform)
	alias(libs.plugins.composeCompiler)
	alias(libs.plugins.valkyrie)
	alias(libs.plugins.ksp)
	alias(libs.plugins.androidx.room3)
}

// remove material 2
configurations.all {
	exclude(group = "org.jetbrains.compose.material", module = "material")
	exclude(group = "androidx.compose.material", module = "material")
}

valkyrie {
	packageName = "paige.navic.icons"
	generateAtSync = true
	outputDirectory = layout.buildDirectory.dir("generated/sources/valkyrie")

	iconPack {
		name = "Icons"
		targetSourceSet = "commonMain"

		nested {
			name = "Brand"
			sourceFolder = "brand"
		}

		nested {
			name = "Outlined"
			sourceFolder = "outlined"
		}

		nested {
			name = "Filled"
			sourceFolder = "filled"
		}
	}
}

val generateBuildInfo = tasks.register("generateBuildInfo", Sync::class) {
	description = "generate BuildInfo.kt"

	val fdroid = providers.gradleProperty("fdroid")
		.map { it.toBoolean() }
		.getOrElse(false)

	from(
		resources.text.fromString(
			"""
			|package paige.navic.generated
			|
			|object BuildInfo {
			|	const val FDROID = $fdroid
			|}
			|
			""".trimMargin()
		)
	) {
		rename { "BuildInfo.kt" }
		into("paige/navic/generated")
	}

	into(layout.buildDirectory.dir("generated/buildInfo/commonMain/kotlin"))
}

val generateIosWorkaround = tasks.register("generateIosWorkaround", Sync::class) {
	// “truly horrifying workaround” for a crash in SearchScreen.kt
	// https://youtrack.jetbrains.com/issue/KT-84055/Reference-to-lambda-in-lambda-in-function-TextField-can-not-be-evaluated#focus=Comments-27-13188532.0-0
	description = "generates a file to workaround a crash on iOS"

	from(
		resources.text.fromString(
			"""
			|package androidx.compose.foundation.text.input
			|
			|import androidx.compose.runtime.Composable
			|
			|public fun interface TextFieldDecorator {
			|	@Suppress("ComposableLambdaParameterNaming")
			|	@Composable
			|	public fun Decoration(innerTextField: @Composable () -> Unit)
			|}
			|
			""".trimMargin()
		)
	) {
		rename { "TextFieldDecorator.kt" }
	}

	into(layout.buildDirectory.dir("generated/iosWorkaround/commonMain/kotlin"))
}

tasks.withType<KotlinCompilationTask<*>>().configureEach {
	dependsOn("generateValkyrieImageVector")
	dependsOn(generateBuildInfo)
	dependsOn(generateIosWorkaround)
}

// no idea why ksp tasks depend on valkyrie
tasks.withType<KspAATask>().configureEach {
	dependsOn("generateValkyrieImageVector")
}

kotlin.sourceSets.commonMain {
	kotlin.srcDir(generateBuildInfo.map { it.destinationDir })
	kotlin.srcDir(generateIosWorkaround.map { it.destinationDir })
}

kotlin {
	listOf(
		iosArm64(),
		iosSimulatorArm64()
	).forEach { target ->
		target.binaries.framework {
			baseName = "ComposeApp"
			isStatic = true
		}
	}

	android {
		namespace = "paige.navic"
		compileSdk = libs.versions.android.compileSdk.get().toInt()
		minSdk = libs.versions.android.minSdk.get().toInt()

		androidResources.enable = true

		compilerOptions {
			jvmTarget.set(JvmTarget.JVM_21)
		}

		packaging {
			resources {
				excludes += "/okhttp3/**"
				excludes += "/*.properties"
				excludes += "/org/antlr/**"
				excludes += "/com/android/tools/smali/**"
				excludes += "/org/eclipse/jgit/**"
				excludes += "/META-INF/versions/9/OSGI-INF/MANIFEST.MF"
				excludes += "/org/bouncycastle/**"
				excludes += "/META-INF/{AL2.0,LGPL2.1}"
			}
		}

		buildToolsVersion = "37.0.0"
	}

	sourceSets {
		commonMain.dependencies {
			implementation(libs.bundles.cmp)
			implementation(libs.bundles.ktor)
			implementation(libs.bundles.coil)
			implementation(libs.bundles.cmpThirdParty)
			implementation(libs.bundles.androidx.lifecycle)
			implementation(libs.bundles.room)
			implementation(libs.bundles.koin)

			implementation(libs.androidx.navigation3.ui)
			implementation(libs.kotlinx.datetime)
			implementation(libs.kotlinx.serialization.json)
			implementation(libs.kotlinx.collections.immutable)
			implementation(libs.androidx.datastore.preferences)
			implementation(libs.coil.gif)

			implementation(libs.subsonicKotlin)
		}

		androidMain.dependencies {
			implementation(libs.bundles.ktor.android)
			implementation(libs.bundles.androidx.android)
			implementation(libs.bundles.media3)
		}

		iosMain.dependencies {
			implementation(libs.bundles.ktor.ios)
		}
	}

	compilerOptions {
		freeCompilerArgs.addAll("-Xexpect-actual-classes", "-Xexplicit-backing-fields")
	}
}

room3 {
	schemaDirectory("$projectDir/schemas")
}

dependencies {
	add("kspAndroid", libs.androidx.room3.compiler)
	add("kspIosSimulatorArm64", libs.androidx.room3.compiler)
	add("kspIosArm64", libs.androidx.room3.compiler)

	add("kspCommonMainMetadata", libs.androidx.room3.compiler)
}
